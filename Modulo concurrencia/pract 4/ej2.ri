{Productores y consumidores.
Productor: recorren av y cada 5 papeles que junta, los deposita en (50,50) 

Consumidor: 
- intenta tomar entre 2 y 5 papeles de esq (50,50), si hay menos lo deja 
- deposita en esq de origen
- para cuando intentÃ³ tomarpap y no lo logra 8 veces seguidas
}

programa ej2
procesos
  proceso depositarP (E cantP:numero)
  comenzar
    repetir cantP
      depositarPapel
  fin
  proceso juntaYdeposita (ES cantP:numero)
  variables 
    avAct,caAct:numero
  comenzar
    mientras (HayPapelEnLaEsquina)  {}
      tomarPapel
      cantP:=cantP+1
      si (cantP = 5) {solo deposita si cantP=5}
        avAct:=PosAv
        caAct:=PosCa
        repetir cantP  {deposita de a un papel}
          BloquearEsquina(50,50)
          Pos(50,50)
          depositarPapel
          Pos(avAct, caAct)
          LiberarEsquina(50,50) 
        cantP:=0  {reinicio contador papeles}
  fin
areas
  fuente: AreaC (50,50,50,50)
  areap1: AreaP (5,1,5,100)
  areap2: AreaP (10,1,10,100)
  areac1: AreaP (11,1,11,1)
  areac2: AreaP (12,1,12,1)
robots
  {recorre av y cada 5 pap que junta deposita en 50,50 y vuelve}
  robot productor 
  variables
    cantP:numero
  comenzar
    cantP:=0
    repetir 99   {repetir 99 recomendado}
      juntaYdeposita (cantP)   {proceso esquina}     
      mover
    juntaYdeposita(cantP) {ultima esquina}
  fin
  robot consumidor
  variables 
    cant,cantP:numero
    veces:numero
    avIni,caIni:numero
  comenzar
    veces:=0
    avIni:=PosAv
    caIni:=PosCa
    mientras (veces <> 8)
      Random(cant,2,5)   {cant papeles a juntar}
      cantP:=0           {papeles juntados}
      BloquearEsquina(50,50)
      Pos(50,50)
      mientras(HayPapelEnLaEsquina) & (cantP<>cant) {puede no haber cantidad pedida}
        tomarPapel
        cantP:=cantP+1
      si (cantP < cant)
        repetir cantP     {dejar los papeles, no alcanzan}
          depositarPapel
      Pos(avIni,caIni)
      LiberarEsquina(50,50)
      si (cantP = cant)   {al salir evalua de nuevo}
        depositarP(cant) {depositar cantP papeles en la esq orig}
        veces:= 0
      sino 
        veces:= veces + 1 
  fin
variables 
  p1: productor
  p2: productor
  c1: consumidor
  c2: consumidor
comenzar
  AsignarArea(p1, areap1)
  AsignarArea(p1, fuente)
  AsignarArea(p2, areap2)
  AsignarArea(p2, fuente)
  AsignarArea(c1, areac1)
  AsignarArea(c1, fuente)
  AsignarArea(c2, areac2)
  AsignarArea(c2, fuente)
  Iniciar(p1,5,1)
  Iniciar(p2,10,1)
  Iniciar(c1,11,1)
  Iniciar(c2,12,1)
fin
