{Turno C - tema 2.}
programa parcial
procesos
  proceso asignaIds 
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
  fin
  proceso finalizar
  comenzar
    EnviarMensaje(0, r1)
    EnviarMensaje(0, r2)
    EnviarMensaje(0, r3)
  fin
  proceso depositarFlores(E cantf:numero; E avIni:numero; E caIni:numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    repetir cantf    {menciona depositar TODAS, pero no de a una}
      depositarFlor
    Pos(avIni,caIni)
    LiberarEsquina(10,10)
  fin
areas
  cuadrante: AreaPC (40,40,60,60)
  fuente: AreaPC (10,10,10,10)
  area1: AreaP (1,1,1,1)
  area2: AreaP (2,1,2,1)
  area3: AreaP (3,1,3,1)
  areaf: AreaP (4,1,4,1)
robots
  robot florero
  variables   
    id, cantf:numero
    av,ca,avIni,caIni: numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    cantf:=0
    RecibirMensaje(id, f)
    RecibirMensaje(av, f)
    mientras (av <> 0)
      RecibirMensaje(ca, f)
      BloquearEsquina(av,ca)
      Pos(av,ca)
      mientras (HayFlorEnLaEsquina)
        tomarFlor
        cantf:= cantf+1
      Pos(avIni,caIni)
      LiberarEsquina(av,ca)
      RecibirMensaje(av,f)
    depositarFlores(cantf,avIni,caIni) 
    EnviarMensaje(cantf, f)
  fin
  robot fiscal
  variables 
    r, av,ca:numero
    totF, cantf:numero
  comenzar
    asignaIds
    repetir 8
      Random(r, 1,3)
      Random(av,40,60)
      Random(ca,40,60)
      si (r=1)
        EnviarMensaje(av, r1)
        EnviarMensaje(ca, r1)
      sino 
        si (r=2)
          EnviarMensaje(av, r2)
          EnviarMensaje(ca, r2)
        sino 
          EnviarMensaje(av, r3)
          EnviarMensaje(ca, r3)
    finalizar 
    totF:=0
    repetir 3
      RecibirMensaje(cantf, *)
      totF:= totF + cantf
    Informar('Total de flores ', totF)
  fin
variables
  r1: florero
  r2: florero
  r3: florero
  f: fiscal
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r1, cuadrante)
  AsignarArea(r1, fuente)
  AsignarArea(r2, area2)
  AsignarArea(r2, cuadrante)
  AsignarArea(r2, fuente)
  AsignarArea(r3, area3)
  AsignarArea(r3, cuadrante)
  AsignarArea(r3, fuente)
  AsignarArea(f, areaf)
  Iniciar(r1, 1,1)
  Iniciar(r2, 2,1)
  Iniciar(r3, 3,1)
  Iniciar(f, 4,1)
fin