{ Turno N.}
programa parcial
procesos
  proceso asignaIds
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
  fin
  proceso actualizaMax(E cantp:numero;ES cantpMax:numero;E id:numero; ES idMax:numero)
  comenzar
    si (cantp>cantpMax)
      cantpMax:=cantp
      idMax:= id
  fin
  proceso depositarP (E cantp:numero)
  variables 
    avAct,caAct:numero
  comenzar
    avAct:=PosAv
    caAct:=PosCa
    BloquearEsquina(100,100)
    Pos(100,100)
    repetir cantp
      depositarPapel
    Pos(avAct,caAct)
    LiberarEsquina(100,100)
  fin
  proceso escalon(E alto:numero; E ancho:numero; ES cantp:numero)
  comenzar
    repetir alto
      juntarP (cantp)
      mover
    derecha
    repetir ancho
      juntarP(cantp)
      mover
    izq 
  fin
  proceso izq
  comenzar
    repetir 3
      derecha
  fin
  proceso juntarP (ES cantp:numero)
  comenzar
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      cantp:=cantp+1
  fin
  proceso asignaDim
  variables 
    r,ancho,alto:numero
  comenzar
    r:=1
    repetir 3
      Random(alto,1,4)
      Random(ancho,1,3)
      si (r=1)
        EnviarMensaje(alto,r1)
        EnviarMensaje(ancho,r1)
      sino 
        si (r=2)
          EnviarMensaje(alto,r2)
          EnviarMensaje(ancho,r2)
        sino 
          EnviarMensaje(alto,r3)
          EnviarMensaje(ancho,r3)
      r:=r+1
  fin
areas
  area1: AreaP (2,1,11,13)
  area2: AreaP (32,1,41,13)
  area3: AreaP (62,1,71,13)
  areaf: AreaP (1,1,1,1)
  fuente: AreaPC (100,100,100,100)
robots
  robot papelero
  variables 
    id, cantp:numero
    alto,ancho:numero
  comenzar
    cantp:=0
    RecibirMensaje(id,f)
    RecibirMensaje(alto,f)
    RecibirMensaje(ancho,f)
    repetir 3
      escalon(alto,ancho,cantp)  {realiza escalera}
    depositarP (cantp)            {deposita en esq}
    EnviarMensaje(id, f)
    EnviarMensaje(cantp, f)
  fin
  robot fiscal
  variables 
    cantp, cantpMax, id, idMax:numero
  comenzar
    cantpMax:=-1
    asignaIds
    asignaDim
    repetir 3
      RecibirMensaje(id,*)
      si (id=1)
        RecibirMensaje(cantp, r1)
      sino
        si (id=2)
          RecibirMensaje(cantp,r2)
        sino 
          RecibirMensaje(cantp,r3)
      actualizaMax(cantp, cantpMax, id, idMax)
    Informar('r junto mas pap', idMax)
  fin
variables
  r1: papelero
  r2: papelero
  r3: papelero
  f: fiscal
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r1, fuente)
  AsignarArea(r2, area2)
  AsignarArea(r2, fuente)
  AsignarArea(r3, area3)
  AsignarArea(r3, fuente)
  AsignarArea(f, areaf)
  Iniciar(f, 1,1)
  Iniciar(r1,2,1)
  Iniciar(r2,32,1)
  Iniciar(r3,62,1)
fin
